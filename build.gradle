buildscript {
    ext {
        grailsVersion = '3.1.0.BUILD-SNAPSHOT'
        gormVersion = System.getenv('GORM_VERSION') ?: '5.0.0.BUILD-SNAPSHOT'
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {        
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath 'com.bertramlabs.plugins:asset-pipeline-gradle:2.5.0'
        classpath "org.grails.plugins:hibernate4:$gormVersion"
        classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
    }
}

ext {
    gormVersion = System.getenv('GORM_VERSION') ?: '5.0.0.BUILD-SNAPSHOT'
    def coreBuildFilePath = 'https://raw.githubusercontent.com/grails/grails-core/master/build.gradle'
    def coreBuildFileUrl = new URL(coreBuildFilePath)
    def versionNumberRegex = /^\s*grailsVersion\s*=\s*['|"](.*)['|"]\s*$/
    def grailsVersionList = []
    coreBuildFileUrl.eachLine { line ->
        def matcher = (line =~ versionNumberRegex)
        if(matcher) {
            grailsVersionList << matcher[0][1]
        }
    }
    if(!grailsVersionList) {
        throw new GradleException("Could not find Grails version at $coreBuildFilePath")
    } else if(grailsVersionList.size() > 1) {
        throw new GradleException("Multiple Grails versions (${grailsVersionList}) found at $coreBuildFilePath")
    }
    grailsVersion = grailsVersionList[0]    
}

println "GORM VERSION: $gormVersion"
subprojects { Project project ->

    group "grails.gorm.functional.tests"

    apply plugin: "io.spring.dependency-management"
    apply plugin: "spring-boot"
    apply plugin: "war"
    apply plugin: "asset-pipeline"
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    if(project.name.contains('plugins/')) {
        apply plugin: "org.grails.grails-plugin"
    }
    else {
        apply plugin: "org.grails.grails-web"
        apply plugin: "org.grails.grails-gsp"
    }

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if(details.requested.group == 'org.grails' && details.requested.name.startsWith('grails-datastore')) {
                details.useVersion(gormVersion)
            }
        }        
    }

    ext {
        grailsVersion = project.grailsVersion
        gradleWrapperVersion = project.gradleWrapperVersion
    }

    assets {
        minifyJs = true
        minifyCss = true
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://repo.grails.org/grails/plugins3" }
    }

    dependencyManagement {
        imports {
            mavenBom "org.grails:grails-bom:$grailsVersion"
        }
        applyMavenExclusions false
    }

    version = "1.0.0"
    dependencies {
        compile "org.springframework.boot:spring-boot-starter-logging"
        compile "org.springframework.boot:spring-boot-starter-actuator"
        compile "org.springframework.boot:spring-boot-autoconfigure"
        compile "org.springframework.boot:spring-boot-starter-tomcat"
        compile "org.grails:grails-dependencies"
        compile "org.grails:grails-web-boot"

        compile "org.grails.plugins:cache"        
        compile "org.grails.plugins:scaffolding"

        runtime "org.grails.plugins:asset-pipeline"

        testCompile "org.grails:grails-plugin-testing"
        testCompile "org.grails.plugins:geb"

        // Note: It is recommended to update to a more robust driver (Chrome, Firefox etc.)
        testRuntime 'org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1'
        testRuntime 'net.sourceforge.htmlunit:htmlunit:2.18'

        console "org.grails:grails-console"
    }


}
task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}    